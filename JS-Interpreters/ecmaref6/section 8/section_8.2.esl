/* Copyright (C) 2022-2025 formalsec programmers
 * 
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

/* 8.2 Code Realms */
/* Before it is evaluated, all ECMAScript code must be associated with a Realm. Conceptually, a realm
   consists of a set of intrinsic objects, an ECMAScript global environment, all of the ECMAScript code
   that is loaded within the scope of that global environment, and other associated state and resources.

   A Realm is specified as a Record with the fields specified in Table 21:
*/

/* 8.2.1 CreateRealm ( ) */
/* The abstract operation CreateRealm with no arguments performs the following steps: */
function CreateRealm() {
  /* Let realmRec be a new Record. */
  realmRec := {};
  /* Perform CreateIntrinsics(realmRec). */
  CreateIntrinsics(realmRec);
  /* Set realmRec.[[globalThis]] to undefined. */
  realmRec.globalThis := 'undefined;
  /* Set realmRec.[[globalEnv]] to undefined. */
  realmRec.globalEnv := 'undefined;
  /* Set realmRec.[[templateMap]] to a new empty List. */
  realmRec.templateMap := [];
  /* Return realmRec. */
  return realmRec;
}

/* 8.2.2 CreateIntrinsics ( realmRec ) */
function CreateIntrinsics(realmRec) {
  /* Initializing global symbols */
  initWellKnownSymbols();
  /* Let intrinsics be a new Record. */
  intrinsics := {};
  |Intrinsics| := intrinsics;
  /* Set realmRec.[[intrinsics]] to intrinsics. */
  realmRec.intrinsics := intrinsics;
  /* Let objProto be ObjectCreate(null). */
  objProto := ObjectCreate('null, null);
  /* Set intrinsics.[[%ObjectPrototype%]] to objProto. */
  intrinsics.ObjectPrototype := objProto;
  /* Let funcProto be CreateBuiltinFunction(realmRec, noSteps, objProto). */
  funcProto := initFunctionPrototype(objProto, realmRec, false);
  /* Set intrinsics.[[%FunctionPrototype%]] to funcProto. */
  intrinsics.FunctionPrototype := funcProto;
  /* Let thrower be CreateBuiltinFunction(realmRec, throwerSteps, null). */
  thrower := CreateGeneralBuiltInFunction(funcProto, [], "internalTypeErrorThrower", realmRec, false, null);
  /* Set intrinsics.[[%ThrowTypeError%]] to thrower. */
  intrinsics.ThrowTypeError := thrower;
  /* Perform AddRestrictedFunctionProperties(funcProto, realmRec). */
  AddRestrictedFunctionProperties(funcProto, realmRec);
  /* Initializing Intrinsics */
  initIntrinsics(intrinsics, realmRec, false);
  /* Return intrinsics. */
  return intrinsics;
}

/* 8.2.3 SetRealmGlobalObject ( realmRec, globalObj ) */

/* The abstract operation SetRealmGlobalObject with arguments realmRec and globalObj performs
   the following steps: */
function SetRealmGlobalObject(realmRec, globalObj) {
  /* If globalObj is undefined, then */
  if (globalObj == 'undefined) {
    /* Let intrinsics be realmRec.[[intrinsics]]. */
    intrinsics := realmRec.intrinsics;
    /* Let globalObj be ObjectCreate(intrinsics.[[%ObjectPrototype%]]). */
    globalObj := ObjectCreate(intrinsics.ObjectPrototype, null);
    |global| := globalObj;
  }

  /* Assert: Type(globalObj) is Object. */
  assert (Type(globalObj) == "Object");
  /* Set realmRec.[[globalThis]] to globalObj. */
  realmRec.globalThis := globalObj;
  /* Let newGlobalEnv be NewGlobalEnvironment(globalObj). */
  newGlobalEnv := NewGlobalEnvironment(globalObj);
  /* Set realmRec.[[globalEnv]] to newGlobalEnv. */
  realmRec.globalEnv := newGlobalEnv;
  /* Return realmRec. */
  return realmRec;
}

/* 8.2.4 SetDefaultGlobalBindings ( realmRec ) */

/* The abstract operation SetDefaultGlobalBindings with argument realmRec performs the following
   steps: */
function SetDefaultGlobalBindings(realmRec) {
  /* Let global be realmRec.[[globalThis]]. */
  global := realmRec.globalThis;

  /* For each property of the Global Object specified in clause 18, do */
    /* Let name be the String value of the property name. */


    /* Let desc be the fully populated data property descriptor for the property containing the
        specified attributes for the property. For properties listed in 18.2, 18.3, or 18.4 the value
        of the [[Value]] attribute is the corresponding intrinsic object from realmRec. */

    /* Let status be DefinePropertyOrThrow(global, name, desc). */

    /* ReturnIfAbrupt(status). */

  initGlobalObject(global, realmRec.intrinsics);
  /* Return global. */
  return global;
}
