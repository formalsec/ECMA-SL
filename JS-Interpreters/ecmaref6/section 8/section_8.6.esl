/* Copyright (C) 2022-2025 formalsec programmers
 * 
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

/* Auxiliary functions */
function NewECMAScriptObject() {
  return {
    internalSlotsList: [],
    JSProperties: {},
    JSPropertiesSymbols: {},
    SymbolKeys: {},
    Class: "Object",
    Extensible: true,
    Prototype: 'null
  };
}

function NewECMAScriptObjectFull(prototype, class, extensible) {
  object := NewECMAScriptObject();
  setInternalProperty(object, "Prototype", prototype);
  setInternalProperty(object, "Class", class);
  setInternalProperty(object, "Extensible", extensible);
  return object;
}

/* Returns a list containing the names of each own property of object. */
function getOwnProperties(object) {
  props := l_sort (obj_fields object.JSProperties);
  syms := [];

  foreach (P : obj_fields object.SymbolKeys)
    syms := l_add(syms, object.SymbolKeys[P]);

  props := l_concat(props, syms); /* cannot sort symbol values only strings */
  return props;
}

/* Returns a list containing the names of each enumerable own property of object. */
function getOwnEnumerableProperties(object) {
  ownProperties := getOwnProperties(object);
  ownEnumerableProperties := [];
  foreach (pname : ownProperties) {
    propDesc := getJSProperty(object, pname);
    if (isEnumerablePropertyDescriptor(propDesc))
      ownEnumerableProperties := l_add(ownEnumerableProperties, pname);
  }
  return ownEnumerableProperties;
}

function getInternalProperty(object, pname) {
  return object[pname];
}

function getJSProperty(object, pname) {
  if (Type(pname) == "Symbol") {
    desc := int_to_string pname._id; /* more things can be added to make debugging easier if necessary */
    return object.JSPropertiesSymbols[desc];
  }
  return object.JSProperties[pname];
}

function setJSProperty(object, pname, pvalue) {
  if (Type(pname) == "Symbol") {
    desc := int_to_string pname._id;
    object.JSPropertiesSymbols[desc] := pvalue;
    object.SymbolKeys[desc] := pname;
  } else {
    object.JSProperties[pname] := pvalue;
  }

  /* para ter ordem de insercao */
  if (!(in_list(pname, object.internalSlotsList)) )
    object.internalSlotsList := l_add(object.internalSlotsList, pname);

  return;
}

function deleteJSProperty(object, pname) {
  if (Type(pname) == "Symbol") {
    desc := int_to_string pname._id;
    delete (object.JSPropertiesSymbols)[desc];
    delete (object.SymbolKeys)[desc];
  } else {
    delete (object.JSProperties)[pname];
  }

  newInternalSlots := [];
  foreach (name : object.internalSlotsList)
    if (name != pname)
      newInternalSlots := l_add(newInternalSlots, name);
  object.internalSlotsList := newInternalSlots;
  return;
}

function inJSProperties(object, pname) {
  if (Type(pname) == "Symbol") {
    desc := int_to_string pname._id;
    return desc in_obj object.SymbolKeys;
  } else {
    return pname in_obj object.JSProperties;
  }
}
