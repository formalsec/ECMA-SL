/* TODO: move __concatStringList to summaries file */
function __concatStringList(globalObject, this, NewTarget, strict, params) {
  return s_concat(params)
}

function newFunctionObject(target, realm) {
  return CreateBuiltInFunctionObject([], target, realm, false, null)
}

function newLazyObject() {
  realm := { globalEnv : |global| };
  lazyObject := newFunctionObject("lazyConstructor", realm);
  lazyObject.Get := "lazyGet";
  /* lazyObject.Call := "lazyCall"; */
  lazyObject.History := [];

  toPrim := newFunctionObject("lazyToPrimitive", realm);
  toPrimDesc := newDataPropertyDescriptorFull(toPrim, false, false, true);
  toPrimProp := getToPrimitivePropertyName();
  setJSProperty(lazyObject, toPrimProp, toPrimDesc);

  format := newFunctionObject("__concatStringList", realm);
  createBuiltInProperty(lazyObject, "format", format);

  ph := newFunctionObject("lazyPrintHistory", realm);
  createBuiltInProperty(lazyObject, "printHistory", ph);

  return lazyObject
}

function lazyConstructor(globalObject, this, NewTarget, strict, params) {
  F := topExecStack().Function;
  newObj := newLazyObject();
  newObj.History := l_prepend(("call", params), F.History);
  return newObj
}

function lazyGet(O, P, Receiver) {
  desc := getJSProperty(O, P);
  if (desc != 'undefined) {
    return desc.Value
  };
  newObj := newLazyObject();
  newObj.History := l_prepend(P, O.History);
  newObjDesc := newDataPropertyDescriptorFull(newObj, true, true, true);
  setJSProperty(O, P, newObjDesc);
  return newObj
}

function lazyPrintHistory(globalObject, this, NewTarget, strict, params) {
  print this.History;
  return
}

function lazyToPrimitive(globalObject, this, NewTarget, strict, params) {
  hint := l_nth(params, 0);
  if (hint = "string") {
    return extern str_symbol("")
  } elif (hint = "number") {
    return extern flt_symbol("")
  } else {
    return extern lift_symbols(
      [ extern str_symbol("")
      , extern flt_symbol("")
      , extern bool_symbol("")
      ]
    )
  }
}
